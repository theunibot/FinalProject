//This is the R12 Robot Init File. This file is where commands are placed in the RoboForth
//Language and are called upon startup by the R12Operations object. 
//Each command should be separated by a carriage return.
//All comments must be on their own line and start with "//"


// Patch the controller to support a deeper stack depth (about 70-100 items).  This is
// no longer be needed since we are not pushing tons of route points onto the stack...
//ORG JUMP
//3FF0 1361 ! 3FF0 8C3 ! 3FF0 8CB ! 3FF0 45B !
//ORG JUMP

// start up the controller with default values
ROBOFORTH
START
DECIMAL
ABSOLUTE
CARTESIAN
2000 TON !
800 TGRIP !

// adjust axis 5 encoder tolerance
110 ENCTOLS 6 + !
-305 ENCRATIOS 8 + !

// declare variable DRI (Dynamic Route Index) to index where we are inside a
// dynamic route construction
VARIABLE DRI
// declare the route name we will use for dynamic routes
ROUTE DR
100 RESERVE

// DRINIT - Dynamic route, phase 1: initializes and clears the dynamic route
: DRINIT
ERASE
0 DRI !
." Dynamic route has been reset" CR
;

// DRPOINT - learns a single point on the dynamic route path.  Stack should contain 6 values - XYZPYR  RYPZYX (x is top most value)
: DRPOINT
//." Filling route index " DRI @ . CR
// increment to the next index in the route list
DRI @ 1 + DRI !
// set the total moves in the route table to support the new line being added
DRI @ MOVES E!
// go store the values into the route line
6 0 DO
   DRI @ LINE I 2* + E!
LOOP
// set the route rules.  
// Bytes 12/13 are: 0=joint values, 1=relative joint coordinate, 2=cartesian, 3=relative cartesian,
// or, the CFA of a word to execute (see RoboForth manual on CFA for examples, and see
// the FIND and SET commands (such as FIND cmd EXECUTE or FIND cmd var ! to store it and then var EXECUTE to execute it)
2 DRI @ LINE 12 + E!
// must be a zero (reference to objects, which we don't use)
0 DRI @ LINE 14 + E!
;

// DRSTART - start executing an async route
: DRSTART
DRY RUN
//CRUN
;

// DRDRY - execute route DRY to determine speed
: DRDRY
DRY ADJUST RUN
;

// DRCOMPLETE - finish execution of an async route
: DRCOMPLETE
?STOP ( wait for the last route to complete )
DSPASSUME ( read current position from DSP )
FSTOP 1 = IF STOPABORT THEN ( if stopped, cause error )
ENCCHECK ( make sure encoders are ok )
;


// ****** THE FOLLOWING ARE OBSOLETE OR NEARLY OBSOLETE ******

// DRSPEED - learn a speed value on the dynamic route
// set up variable S to have the CFA of the SPEED command
VARIABLE S
FIND SPEED S !
: DRSPEED
// increment DRI variable to indicate our index in the route table
DRI @ 1 + DRI !
// adjust MOVES to reflect the total lines in route
DRI @ MOVES E!
// take value on stack and store in first entry in line (speed value)
DRI @ LINE E!
// take CFA of SPEED command and store in position 12 (to execute SPEED command)
S @ DRI @ LINE 12 + E!
// make sure 14 is zero (we don't use objects)
0 DRI @ LINE 14 + E!
;


// DRRUN - run the dynamic route, with adjust
: DRRUN
//DR
." Dynamic route contains:" CR
L.
DRY RUN
;

// DRTEST - test the dyamic route
: DRTEST
//DR
." Dyamic route contains:" CR
L.
DRY ADJUST RUN
;
